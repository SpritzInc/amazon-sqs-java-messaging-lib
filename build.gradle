apply plugin: 'java'
apply plugin: 'maven-publish'

group = 'com.amazonaws'
version = '1.0.2-Spritz'

description = "Amazon SQS Java Messaging Library"

// Since we're specifically targeting Java 7, we're also specifying the compiler bootclasspath below.
sourceCompatibility = 1.7
targetCompatibility = 1.7

// Default release to false.  To perform a release build run gradle with -Prelease=true
if (!project.ext.has("release")) {
    project.ext["release"] = false
}

if (!release) {
    // Unless it's a release build, append -SNAPSHOT
    project.version = project.version + "-SNAPSHOT"
}

println "Project: ${project.name}, Version: ${project.version}"

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.amazonaws', name: 'aws-java-sdk-sqs', version:'1.11.30'
    compile group: 'org.apache.geronimo.specs', name: 'geronimo-jms_1.1_spec', version:'1.1.1'
    testCompile group: 'junit', name: 'junit', version:'4.11'
    testCompile group: 'org.mockito', name: 'mockito-core', version:'1.10.8'
}

tasks.withType(JavaCompile) {
    options.fork = true

    if (project.ext.has('java_1_7_bootClasspath')) {
        // When cross-compiling with/for a different version, Gradle complains unless you define a bootClasspath.
        // If defined, use the alternate boot classpath.  It's possible to define it elsewhere
        // (like ~/.gradle/gradle.properties).  An example definition is:
        // java_1_7_bootClasspath=/opt/java/1.7/rt.jar:/opt/java/1.7/jce.jar
        options.bootClasspath = "${project.ext.java_1_7_bootClasspath}"
    }

    options.encoding = 'UTF-8'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // Publish to our public repository
    repositories {
        maven {
            credentials {
                username "${spritz_nexus_username}"
                password "${spritz_nexus_password}"
            }

            url "https://nexus.spritzinc.com/content/repositories/Public${release ? 'Releases' : 'Snapshots'}/"
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.4.1'
}
